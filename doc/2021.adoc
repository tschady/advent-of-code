= Advent of Code 2021
:author: @tschady
:toc:
:toc-placement!:
:date: 2021 Dec 01
:description: Commentary on solutions to http://adventofcode.com/2021[Advent of Code 2021]
:sectanchors:
:stem:

ifdef::env-github[]
:tip-caption: :bulb:
:note-caption: :information_source:
:important-caption: :heavy_exclamation_mark:
:caution-caption: :fire:
:warning-caption: :warning:
endif::[]

My running commentary on approach, with any code of interest.

== Rules for Sanity

1. Never stay up for problem release.
1. Don't sweat the leaderboard.
1. Finish puzzle and refactor before looking at other approaches.

toc::[]

== link:../src/aoc/2021/d01.clj[aoc.2021.d01]
The trick here is realizing that `(B + C + D) - (A + B + C) = D - A`.
I originally used the 3-arity version of map with
`(count (filter pos? (map - (rest input) input)))`
but then realized that was a utility fn I'd forgotten about: `intervals`, for
the speed competition I never enter.

[source, clojure]
----
(defn part-1 [input]
  (count (filter pos? (intervals input))))

(defn part-2 [input]
  (count (filter pos? (intervals 3 input))))
----


== link:../src/aoc/2021/d04.clj[aoc.2021.d04]
I used 2D arrays for cards and `nil` to mark numbers.
I thought `(group-by bingo?)` worked well, but the main point of interest is
the lazy sequence of recursive calls to `scores`. This sets up the elegant
`part-1`, `part-2` solution, which reads like the problem description.

[source, clojure]
----
(defn scores [[[num & nums] cards]]
  (lazy-seq
   (when num
     (let [{winners true remain nil} (->> cards
                                         (mark-all-cards num)
                                         (group-by bingo?))
           win-scores (map (partial score num) winners)]
       (concat win-scores (scores [nums remain]))))))

(defn part-1 [input] (first (scores (parse-input input))))

(defn part-2 [input] (last (scores (parse-input input))))
----


== link:../src/aoc/2021/d05.clj[aoc.2021.d05]
I use slope and y-intercept to compute points on the line. The interesting part is
use of Infinity: `##Inf` which works well mathematically, but does require the occasional 'if'.

[source, clojure]
----
(defn- slope [[[x1 y1] [x2 y2]]]
  (if (= x2 x1)
    ##Inf
    (/ (- y2 y1) (- x2 x1))))
----


This lets me describe horizontal/vertical/diagonal lines by their slope.

[source, clojure]
----
(defn part-2 [input] (solve #{0 ##Inf 1 -1} input))
----


== link:../src/aoc/2021/d06.clj[aoc.2021.d06]
Simple `iterate` on an 8-element vector of rolled-up fish counts per time.
I saw some solutions that completely destructured the `timers` vector.
This is clever - I never think of it - I don't know why.

[source, clojure]
----
(defn next-gen [timers]
  (let [spawners (get timers 0 0)]
    (-> timers
        (subvec 1)
        (assoc 8 spawners)
        (update 6 (fnil + 0) spawners))))
----


== link:../src/aoc/2021/d07.clj[aoc.2021.d07]
Like many of these AOC problems, a HOF is a clean way to separate part-1 and
part-2 formulas.

[source, clojure]
----
(defn total-fuel
  "Returns the total fuel used by all the crabs at locations `locs` to reach point `dest`,
  using function `f` to compute fuel required for each distance."
  [locs f dest]
  (reduce + (map (comp f #(Math/abs (- ^int % ^int dest))) locs)))
----


For part 1, because the costs are linear with distance, we know the median will be the meeting point.

[source, clojure]
----
(defn part-1 [input]
  (total-fuel input identity (m/median input)))
----


For part 2, if the fuel formula was `n^2` then the mean would yield the exact answer.
But since formula is a triangle number, which is always less than the square, I first used the mean as an upper bound to reduce the search space.
Then I found
https://www.reddit.com/r/adventofcode/comments/rawxad/2021_day_7_part_2_i_wrote_a_paper_on_todays/[this beauty]
which shows the answer will be within 0.5 of the mean, so I test the 2 integers around it.

[source, clojure]
----
(defn part-2 [input]
  (let [mean (m/mean input)
        low (int (Math/floor mean))
        hi (int (Math/ceil mean))]
    (->> (range low (inc hi))
         (map (partial total-fuel input m/series-sum))
         (apply min))))
----


== link:../src/aoc/2021/d08.clj[aoc.2021.d08]
For this problem I used a combination of expected frequencies of the whole,
and differences between the characters.

- we recognize 1,4,7,8 from their unique string sizes
- we know {b e f} from their unique freqs in the full set of numbers
- we know {a} from diff of 1 and 7
- we know {c} because it's the unknown remaining in 1
- we know {d} because it's the only unknown left in 4
- we know {g} because it's last

I also wrote a handy string diff function that returns a 3-tuple of [only left, only right, common] modeled after `core.data/diff`

[source, clojure]
----
(defn determine-output [[digits outputs]]
  (let [[one seven four & _] (sort-by count digits)
        all-freq (frequencies (apply str digits))
        b (key-for-val all-freq 6)
        e (key-for-val all-freq 4)
        f (key-for-val all-freq 9)
        a (ffirst (diff seven one))
        c (ffirst (diff one #{f}))
        d (ffirst (diff four #{b c f}))
        g (ffirst (diff "abcdefg" #{a b c d e f}))
        subst-map {a \a b \b c \c d \d e \e f \f g \g}]
    (->> outputs
         (map (comp alphagram (partial replace subst-map)))
         (map #(.indexOf all %))
         str/join
         Long/parseLong)))
----


== link:../src/aoc/2021/d09.clj[aoc.2021.d09]
A straightforward problem.  Notably, I was able to reuse my `grid` library
to build the grid, find neighbors, and create the graph in the form of an adjacency map.

[source, clojure]
----
(defn part-2 [input]
  (let [grid (g/build-grid input #(Character/getNumericValue %))]
    (->> (filter (partial low-point? grid) grid)
         (map first)
         (map (partial g/connected-adjacency-map (partial basin? grid) g/neighbor-coords-news))
         (map count)
         (sort >)
         (take 3)
         (reduce *))))
----


== link:../src/aoc/2021/d11.clj[aoc.2021.d11]
The core `step` function used in `iterate`.  By iterating, we do not need to
track any intermediate state like the zero count since we can sum over all the
states reached.

[NOTE]
====
My approach to these problems is to start from the outside in.  In this case,
I typed `(reduce flash grid flashers)` before anything else.
====

[source, clojure]
----
(defn step [grid]
  (loop [grid (transform [MAP-VALS] inc grid)]
    (if-let [flashers (seq (filter #(> (val %) 9) grid))]
      (recur (reduce flash grid flashers))
      grid)))
----


== link:../src/aoc/2021/d12.clj[aoc.2021.d12]
This problem immediately looked like a recursive Depth First Search.
For part-1 I could track the typical visited nodes and remove them from
the next search level, but interestingly part-2 flipped this on its head
and used a variable count.  This could be tracked with an extra boolean
like `bonus-used?`, but I preferred to put this complexity in the data layer
with the allowances map below.

[source, clojure]
----
(defn dfs-paths [g goal path allowances]
  (let [curr (peek path)]
    (if (= goal curr)
      (vector path)
      (let [nexts (filter #(pos? (get allowances %)) (get g curr))]
        (mapcat #(dfs-paths g goal (conj path %) (update allowances curr dec)) nexts)))))
----


The interesting part of the algorithm is this map of the number of times
each cave may be visited.  I use infinity again for large cave count since it can be decremented forever.

[source, clojure]
----
(defn make-allowances
  "Returns map of cave to number of times it may be visited.
  Small caves begin with lowercase and can be visited once.
  Large caves (everything not small) can be visited infinitely."
  [g]
  (let [{small true, big false} (group-by small-cave? (keys g))]
    (merge (zipmap small (repeat 1)) (zipmap big (repeat ##Inf)))))
----


I optimized for dev time and DFS algo simplicity here, by iterating over
the collection of small-caves, treating each one in turn as
the magic cave that can be visited twice.  There is a lot of duplication here,
with many paths visited multiple times then collpased with `set`.

[source, clojure]
----
(defn part-2 [input]
  (let [g           (parse-graph input)
        init-allow  (make-allowances g)
        small-caves (remove #{"start" "end"} (filter small-cave? (keys g)))]
    (->> small-caves
         (map #(update init-allow % inc))
         (mapcat (partial dfs-paths g "end" ["start"]))
         set
         count)))
----


== link:../src/aoc/2021/d13.clj[aoc.2021.d13]
Great use of specter here to perform a complex conditional mutation.
Thanks to @drowsy for the idea

[source, clojure]
----
(defn fold [paper [axis v]]
  (set (transform [ALL (if (= 'x axis) FIRST LAST) (pred> v)] #(- (* 2 v) %) paper)))
----


== link:../src/aoc/2021/d14.clj[aoc.2021.d14]
For part-1, I raced to an iterative solution building the string each time with
`medley.core/interleave-all`.
10 iterations took 20ms, 20 took 1000x that, so there's no way we can do this 40 times.
Looking at the ruleset, it's pretty contained, so we should be able to just track
counts of each pair.  Very similar to day 6 for fish count.

[source, clojure]
----
(defn step [rules pair-counts]
  (reduce-kv (fn [m [a b :as k] v]
               (let [insert (get rules k)]
                 (-> m
                     (update [a insert] (fnil + 0) v)
                     (update [insert b] (fnil + 0) v))))
             {}
             pair-counts))
----


The only thing of interest here is `x-nth`, a utility function I wrote that just
reverses the arguments of `nth` in order to make thread-last work.

[source, clojure]
----
(defn solve [input n]
  (let [[orig rules] (parse input)]
    (->> orig
         (partition 2 1)
         frequencies
         (iterate (partial step rules))
         (x-nth n)
         (assemble (last orig))
         score)))
----


== link:../src/aoc/2021/d15.clj[aoc.2021.d15]
First graph problem of the year.

[IMPORTANT]
====
(Is your https://www.reddit.com/r/adventofcode/comments/k3q7tr/my_advent_of_code_2020_bingo_card_fun_little_side/[AdventOfCode bingo card] complete yet?)
====

You *could* type out Dijkstra's algorithm, using Java's PriorityQueue
or `clojure.data.priority-map`.  Or, you could just slam it all into
a graph library and Keep 'er Movin'.

My go-to for these is https://github.com/Engelberg/ubergraph[ubergraph].
There was some ambiguity in the constructor for edges
(since the nodes were also `[x y]` vectors),
so I had to build an empty graph and use the explicit `add-edges*`

[source, clojure]
----
(defn edges [risks]
  (for [loc      (keys risks)
        neighbor (grid/neighbor-coords-news loc)
        :let     [risk (get risks neighbor)]
        :when    (some? risk)]
    [loc neighbor {:weight risk}]))

(defn safest-path [risk-grid start end]
  (-> (uber/multidigraph)
      (uber/add-edges* (edges risk-grid))
      (alg/shortest-path start end :weight)))
----


Part 2 is solved the same way after expanding the grid.
My new `mod-1` function helps prevent off-by-ones with 1-based indexing.

[source, clojure]
----
(defn expand-grid [grid magnifier]
  (let [[width height] (grid/size grid)]
    (apply merge (for [loc (keys grid)
                       dx   (range magnifier)
                       dy   (range magnifier)
                       :let [[x y] loc
                             risk (get grid loc)
                             new-risk (mod-1 (+ dx dy risk) 9)]]
                   {[(+ x (* width dx)) (+ y (* height dy))] new-risk}))))
----


Expanding the grid with size 1 for part-1 in an expensive no-op,
but I'm a sucker for generalizing the two parts.

[source, clojure]
----
(defn solve [input magnifier]
  (let [g (-> input
              (grid/build-grid #(Character/digit % 10))
              (expand-grid magnifier))
        end (mapv dec (grid/size g))]
    (:cost (safest-path g [0 0] end))))

(defn part-1 [input] (solve input 1))

(defn part-2 [input] (solve input 5))
----


== link:../src/aoc/2021/d16.clj[aoc.2021.d16]
This problem was tedious and painful.  I considered BNF parsing
(but the branching is pretty complex), and Java ByteBuffers,
before just settling on plain old Clojure.
The _only_ thing I like about this code is my use of `cl-format`.

[source, clojure]
----
(defn hex->bits [hex]
  (cl-format nil "~{~4,'0B~}" (map #(Character/digit % 16) hex)))
----


Since all this data is immutable, all of these `slice-*` fn's
take in a stream and return a tuple of the target value and the
stream with the target removed. Simulates destructive chomping,
but it is absolutely no fun.

[source, clojure]
----
(defn slice-val [n stream]
  (let [[subj stream] (split-at n stream)]
    [(s->int 2 subj) stream]))
----


`medley.core/take-upto` saved some time knowing when to stop
chomping the bits.

[source, clojure]
----
(defn slice-literal [stream]
  (let [val-part (->> stream
                      (partition 5)
                      (take-upto #(= \0 (first %))))
        stream   (drop (count (flatten val-part)) stream)
        value    (->> val-part
                      (map (partial drop 1))
                      flatten
                      (s->int 2))]
    [value stream]))
----


The main loop is OK.

[source, clojure]
----
(defn parse-packet [stream]
  (let [[version stream] (slice-val 3 stream)
        [type stream]    (slice-val 3 stream)
        [payload stream] (case type
                           4 (slice-literal stream)
                           (slice-operator stream))]
    [{:version version :type type :payload payload}
     stream]))
----


Mapping to functions makes the code readable

[source, clojure]
----
(def type->op
  {0 +
   1 *
   2 min
   3 max
   5 #(if (> %1 %2) 1 0)
   6 #(if (< %1 %2) 1 0)
   7 #(if (= %1 %2) 1 0)})

(defn evaluate [{:keys [type payload]}]
  (if (= 4 type)
    payload
    (reduce (type->op type) (map evaluate payload))))
----


== link:../src/aoc/2021/d17.clj[aoc.2021.d17]
Much more fun problem!  More thinking, less typing.
For part 1, we know the probe will return to exactly `y=0` at
some `t` because of symmetry, so the max velocity will be
whatever barely fits in the box at the next step.

[source, clojure]
----
(defn part-1 [[_ _ y0 _]]
  (series-sum (dec (Math/abs y0))))
----


Because dx/dt^2 is a step function (-1, then suddenly 0)
we simplify by capping t to when motion stops.  Y is typical.
TODO: equations

[source, clojure]
----
(defn vx
  "Return the initial x-axis velocity to reach point `x` at time `t`.
  Because the X velocity stops at 0 forever, we determine that time
  with quadratic formula and cap results there."
  [x t]
  (let [t_vx0 (int (Math/round (first (quadratic 1 1 (* -2 x)))))
        t (min t t_vx0)]
    (/ (+ (* 2. x) (* t t) (* -1 t)) (* 2 t))))

(defn vy
  "Return the initial y-axis velocity to reach point `y` at time `t`."
  [y t]
  (/ (+ t -1 (/ (* 2. y) t)) 2))

(defn vel-range
  "Returns the range [endpoints) of velocities that fit in target
  box noted by `p_0 p_1` using velocity function `f` at time `t`."
  [f [p_0 p_1] t]
  [(int (Math/ceil (f p_0 t)))
   (inc (int (Math/floor (f p_1 t))))])
----


The main approach is to parametrize over `t`.  This is closed
to some max `t` which we can calculate with the quadratic formula.
Since x and y motion is independent, we can calculate the velocity
ranges that pass through the target at each time step.

[source, clojure]
----
(defn t-max-y [p]
  (let [max-v (dec (Math/abs p))]
    (int (first (quadratic 1 (* -1 (inc (* 2 max-v))) (* 2 p))))))

(defn part-2 [[x_0 x_1 y_0 y_1]]
  (count (set (for [t (range 1 (inc (t-max-y y_0)))
                    x (apply range (vel-range vx [x_0 x_1] t))
                    y (apply range (vel-range vy [y_0 y_1] t))]
                [x y]))))
----


== link:../src/aoc/2021/d18.clj[aoc.2021.d18]
My first thought was either `tree-seq` or `clojure.zip`.  With all the
navigating necessary, I went with zippers. Although I'm familiar with
them from considering their use on previous years, this was my first
actual problem with them so I spent some time reading.  Then considerable
experimentation to figure out the navigation.

[CAUTION]
====
I lost hours because I read the instructions wrong.  I was navigating through
the tree, finding the first available operation, then doing it.  The instructions
say to do all the exploding, then go back and do any splitting.  I had to use
printf debugging on the example with mine vs. another person's solution to see
where I went wrong.
====

Gotta love problem input that's native Clojure code so `read-string` just works.

[source, clojure]
----
(def input (mapv read-string (file-util/read-lines "2021/d18.txt")))
----


Nice to have the depth along for the ride.

[source, clojure]
----
(defn- explode? [node]
  (and (coll? (zip/node node))
       (= 4 (count (zip/path node)))))
----


I use an iterator to lazily navigate the tree in the specified
direction: `next` for forward/right, `prev` for backwards/left

We need to include this `(not (nil? ...))` check because `zip/end`
only works going forwards.  When we go backwards past the root,
`nil` is our terminating signal.

[source, clojure]
----
(defn iter-zip [zipper step-fn]
  (->> zipper
       (iterate step-fn)
       (take-while #(and (not (nil? %))
                         (not (zip/end? %))))))
----


Since we need to update two elements, we have to return
back to this node only if we updated the left one.  Thus
an ugly kludge in the add-left function to return back.

[source, clojure]
----
(defn explode [zipper]
  (let [[left right] (zip/node zipper)]
    (-> zipper
        (zip/replace 0)
        (add-left left)
        (add-right right)
        zip/root)))

(defn split
  "Returns the zipper with this node replaced by a new child node.
  The childs' values are the integer halves of the current value,
  with rounding going to the right value."
  [zipper]
  (let [n     (zip/node zipper)
        left  (quot n 2)
        right (- n left)]
    (-> zipper
        (zip/replace [left right])
        zip/root)))
----


The main loop uses iterators to find the next available exploder.
If there isn't one, then try and split.  If we don't split, then
we're done.
TODO: This is currently inefficient, as we re-navigate back to each
exploding node, when we could just do all the exploders in turn.
(5s for part-2)

[source, clojure]
----
(defn reduce-snail [data]
  (let [zipper (-> data zip/vector-zip)
        iter   (iter-zip zipper zip/next)]
    (if-let [exploder (find-first explode? iter)]
      (recur (explode exploder))
      (if-let [splitter (find-first split? iter)]
        (recur (split splitter))
        (zip/root zipper)))))
----


== link:../src/aoc/2021/d20.clj[aoc.2021.d20]
Overall approach - use a map of [x y] to contents as usual.
Normally, we could just store the 1s, and omit the zeros, but this
problem has special consideration around Infinity. We start with an
infinite board of darkness, and my input "algorithm" turns any fully
dark square on (index 0 is `#`), and fully light squares off
(index 512 is `0`). On odd iterations, the board will be infinitely bright.
On even, infinitely dark.  If the problem asked for odd iterations,
I could track the bounded dark squares, but since they only ask for even
input I'll skip that and cheat with the `field` value below.  This is
an infinite cycle representing what the outskirts look like at the time.

[source, clojure]
----
(def glyph->val {\# \1 \. \0})

(defn parse [[algo-str img-str]]
  (let [img-lines (str/split-lines img-str)]
    {:low   0
     :hi    (count img-lines)
     :field (cycle [\0 \1])
     :img   (grid/build-grid img-lines glyph->val)
     :algo  (mapv glyph->val algo-str)}))
----


When checking for surrounding values, if it's out of bounds,
it's part of the infinite field.

[source, clojure]
----
(defn new-val [algo img loc default]
  (let [locs (grid/neighbor-coords loc (grid/area-deltas 1))]
    (->> locs
         (map #(get img % default))
         (s->int 2)
         (get algo))))
----


Use a typical step function for iteration.  Caching the current
range extremities in `low` and `hi` saves a bit of time.

[source, clojure]
----
(defn step [{:keys [low hi img algo field] :as state}]
  (let [low  (dec low)
        hi   (inc hi)
        span (range low hi)
        locs (for [x span y span] [x y])]
    (reduce (fn [state loc]
              (assoc-in state [:img loc] (new-val algo img loc (first field))))
            (-> state
                (assoc :low low)
                (assoc :hi hi)
                (update :field rest))
            locs)))

(defn solve [input n]
  (->> (parse input)
       (iterate step)
       (x-nth n)
       :img
       (filter-vals #{\1})
       count))
----


This is pretty slow (5s for part-2 on my machine).
Replacing the `(iterate step state)` with a loop over a transient
should be much faster

== link:../src/aoc/2021/d21.clj[aoc.2021.d21]
In line with my goal of being able to easily produce visualizations,
I avoid recursion (and thus forego memoization benefits) to make
lazy update functions usable with `iterate`.

It wouldn't be practical to generalize the two halves of problem
since we don't need any concept of turn in part 2.

I started with cycles for infinite die roll and next player (i.e. `(def fake-d100 (cycle (range 1 101)))` but changed them out to functions for easier debugging.

[source, clojure]
----
(defn deterministic-die [turn] (+ 3 (* 3 (inc (* 3 turn)))))
----


I used my new `mod-1` function again.

[source, clojure]
----
(defn advance-pos [roll pos] (mod-1 (+ roll pos) 10))
----


The active player is toggled, and used as the index of item to update

[source, clojure]
----
(defn advance-state [{:keys [pos scores player] :as state} roll]
  (let [new-pos (advance-pos roll (get pos player))]
    (-> state
        (update-in [:scores player] + new-pos)
        (assoc-in [:pos player] new-pos)
        (assoc :player (mod (inc player) 2)))))

(defn play-turn [{:keys [turn pos player die-fn] :as game}]
  (-> game
      (advance-state (die-fn turn))
      (update :turn inc)))
----


I like the readability of using `medley.core/find-first` to stop iteration.

[source, clojure]
----
(defn part-1 [input]
  (->> input
       (make-game deterministic-die)
       (iterate play-turn)
       (find-first (partial winner 1000))
       final-score))
----


Part 2 uses same iterative approach, which is much slower.  ~1.2s.
But this gives us the full state at every time tick in a lazy sequence
for viz.

[source, clojure]
----
(def dirac-rolls
  "The possible outcomes by frequency of 3d3."
  (frequencies (for [r1 [1 2 3]
                     r2 [1 2 3]
                     r3 [1 2 3]]
                 (+ r1 r2 r3))))

(defn dirac-poss
  "Return a map of the possible outcome states with their frequency, based on
  every possibility of a 3d3 roll."
  [state n]
  (reduce (fn [state-hash [roll freq]]
            (merge-with + state-hash (hash-map (advance-state state roll) (* n freq))))
          {}
          dirac-rolls))

(defn step-dirac
  "Advance the state of the dirac world by 1 time-tick.  This updates all of our
  current states into their following states in one pass, suitable for `iterate`."
  [win-score world]
  (reduce (fn [world [state n]]
            (let [{p1 0 p2 1 remain nil} (->> (dirac-poss state n)
                                              (group-by #(winner win-score (key %))))]
              (-> world
                  (update-in [:winners 0] + (reduce + (map second p1)))
                  (update-in [:winners 1] + (reduce + (map second p2)))
                  (update :state-hash dissoc state)
                  (update :state-hash #(merge-with + % (into {} remain))))))
          world
          (:state-hash world)))
----


We terminate when there are no more states that aren't winners.

[source, clojure]
----
(defn part-2 [input]
  (->> (make-dirac input)
       (iterate (partial step-dirac 21))
       (find-first #(empty? (:state-hash %)))
       :winners
       (reduce max)))
----


== link:../src/aoc/2021/d22.clj[aoc.2021.d22]
I looked at the data and saw the two halves, I cut/paste in
the obvious first section to not need a clamping function and get
part1 quickly using a naive set of currently lit coordinates.  The
input was small enough.

For part2 this wouldn't work - even with bitpacking, it is TBs to
store the naive solution, so we can't track individual locations.  I
worked out the 2D case on paper to learn the algorithm, which is to
recursively remove all overlapping subcubes from each layer of the
instructions.  For 'on' cubes, we add their unique volume.  For
'off' cubes, they add 0 lights, but still get removed from cubes
before them.

---

These two low level utilities do most of the work to find the
overlapping areas of two cuboids.

[source, clojure]
----
(defn clamp
  "Return the coordinate range `x0` -> `x1` clamped to range `lo` -> `hi`.
  Returns nil in the case of no overlap."
  [[lo hi] [x0 x1]]
  (when (and (<= lo x1) (>= hi x0))
    [(max lo x0) (min hi x1)]))

(defn overlap
  "Return the cuboid intersection of `c1` and `c2`, else nil."
  [c1 c2]
  (let [result (map clamp c1 c2)]
    (when (every? identity result) result)))
----


The core of the algorithm is this recursive method to find
the volume of lights uniquely represented by this cube by
subtracting out the volumes of overlapping cubes in later
instructions.  To make sure we do not subtract the same volume
twice (or more), we need to descend the entire tree recursively. I
borrowed the lazy seq `tails` from Haskell to elegantly get the set
of remaining combinations to consider.  I inelegantly use `butlast`
to prevent nil checks in destructuring, since the last item of
tails is '().

[source, clojure]
----
(defn unique-volume
  "Return the volume unique to `cuboid`.  Recursively remove volumes
  uniquely held by sub-cuboids from those that overlap `cuboid`."
  [cuboid cuboids]
  (let [relevant-cuboids (keep #(overlap cuboid %) cuboids)]
    (reduce (fn [total [this & subcuboids]]
              (- total (unique-volume this subcuboids)))
            (volume cuboid)
            (butlast (tails relevant-cuboids)))))
----


== link:../src/aoc/2021/d24.clj[aoc.2021.d24]

CAUTION: I enjoyed the aha! moment of this puzzle.  Major spoilers
ahead.  It's worth figuring on your own.

Sometimes I'll kick off a brute force search while figuring out the
optimal method.  But with a search space of stem:[10^14], that was
never going to work.  So into the data we go.

=== Observations

I could see the 14 sections (one for each input) differed only on 3
lines:

- 4: `div z {1,26}` (which I label `d`)
- 5: `add x <int>` (labelled `a`)
- 15: `add y <int>`, (labelled `b`)

`d` covaries with `a`, so we don't need to track it.  (`d` is
always 1 if `a` is a positive integer, else 26). We'll call each
input stem:[m_i], for "m"odel.

.*Equations embedded in the algorithm*
* Match: stem:[m_i = z_(i-1) mod 26 + a_i]
* Embiggening: stem:[z_i = 26 * z_(i-1) + m_i + b_i]
* Shrinkulation: stem:[z_i = floor(z_(i-1) -: 26)]

Also:

- `w` is never modified, it only gets the current input
- `x` and `y` are zeroed out at the start of each run
- `z` is the acculumator
- `x` is either 0 if there is a Match, else 1.
- `a` is never from 1-9, so no positive `a` can result in a Match
- stem:[AA x, (26 * x) mod 26 = 0]

Therefore there are two things that can happen:

1. no Match (a > 9): `z` gets Embiggened
1. Match (a < 1): `z` gets Shrinkulated

For `z` to be zero after 14 runs, there needs to be an equal number
of embiggenings and shrinkulations.  For my input, this means all
possible matches are matches.

On to the code.

---

We only care about `a` and `b`, so cherry-pick them from the
input.

[source, clojure]
----
(defn parse [input]
  (->> input
       (partition 18)
       (map (juxt #(nth % 5) #(nth % 15)))
       (map (partial apply str))
       (map string-util/ints)))
----


=== Key idea

By stepping through the data, I could see that embiggening rolls up
the previous `z` inside a multiplication with 26, which hides it
once mod 26 occurs, and adds in new information: stem:[m_i + b_i].
Shrinkulation destroys that last bit of new info by dividing by 26
and truncating, and simultaneously exposes the last information
added.  OMG it's a stack, what a great puzzle.

[source, clojure]
----
(defn det-digit-rels
  "Returns a collection of `[i1, i2, n]` tuples, where `n` is the
  difference between the two indices `i1`, `i2` of the model number"
  [data]
  (loop [data data
         i 0
         stack []
         rels []]
    (if-let [[a b] (first data)]
      (cond
        (< 9 a) (recur (next data) (inc i) (conj stack [i b]) rels)
        (> 1 a) (let [[i0 b0] (peek stack)]
                  (recur (next data) (inc i) (pop stack) (conj rels [i i0 (+ a b0)]))))
      rels)))
----


With the known differences between number pairs, this
function finds the integer pair that satisfies the max = 9, min = 1
target of parts 1 and 2.

[source, clojure]
----
(defn rel->nums
  "Returns a tuple of two digits, [a b], where b = a + diff,
  one of a or b is the target, and both numbers are from 1->9.
  e.g. (rel->nums 9 4)  => [5 9]
       (rel->nums 9 -2) => [9 7]
       (rel->nums 1 5)  => [1 6]"
  [target diff]
  (if (< 0 (+ target diff) 10)
    [(+ target diff) target]
    [target (- target diff)]))

(defn build-model
  "For a given `target` number (9 if we're trying for highest, 1 for lowest),
  and pairwise digit relations `rels`, a collection of [d0 d1 diff]
  tuples, return the model number that fits all digit relations,
  trending towards the target."
  [target rels]
  (reduce (fn [n [i0 i1 diff]]
            (let [[v0 v1] (rel->nums target diff)]
              (assoc n i0 v0 i1 v1)))
   (vec (repeat 14 0))
   rels))

(defn solve [input target]
  (->> (parse input)
       det-digit-rels
       (build-model target)
       (apply str)))

(defn part-1 [input] (solve input 9))

(defn part-2 [input] (solve input 1))
----


== link:../src/aoc/2021/d25.clj[aoc.2021.d25]
Straightforward reuse of `wrap-coords` to find any blockers.
I intentionally used `dissoc` and `assoc` because I thought
I'd switch to the transient versions later in a loop/recur
for speed.

[source, clojure]
----
(defn shift [dx dy grid g [loc c]]
  (let [new-loc (->> loc
                     (grid/vector-add (->delta c))
                     (grid/wrap-coords dx dy))]
    (if (get grid new-loc)
      g
      (-> g
          (dissoc loc)
          (assoc new-loc c)))))
----


This is my first use of `as->`. I always thought it obfuscating,
but I like it here.

[source, clojure]
----
(defn step [dx dy grid]
  (as-> grid g
    (reduce (partial shift dx dy g) g (filter-vals #{\>} grid))
    (reduce (partial shift dx dy g) g (filter-vals #{\v} grid))))
----


This is really the year of iterate.  I hope I return to these with
visualizations.

[source, clojure]
----
(defn part-1 [input]
  (let [[dx dy grid] (parse input)]
    (->> grid
         (iterate (partial step dx dy))
         (partition 2 1)
         (take-while (partial apply not=))
         count
         inc)))
----
